Quest: JF000017
DisplayName: Star Chamber

QRC:

QuestorOffer:  [1000]
<ce>             %lev, your rank brings you honour and prestige,
<ce>             but all things have a price. Your esteemed title
<ce>             carries with it duties and responsibilities. The
<ce>             brighter the glory, the heavier the burden.
<ce>             
<ce>             In exceptional times, such as these, it is necessary
<ce>             for all men to decide between the letter of the law
<ce>             and the true spirit of justice. In this Order we
<ce>             respect the former but enforce the latter.
<ce>             
<ce>             Which is a very elaborate way of saying that
<ce>             sometimes we must do things which are not strictly
<ce>             legal, but undoubtedly for the common good. We have
<ce>             no shortage of criminals, traitors and renegades that,
<ce>             for one reason or another, have evaded the law and
<ce>             remain at liberty.
<ce>             
<ce>             Will you be the sword of the spirit of justice?
<--->
<ce>             %lev, you have risen to the higher ranks of this
<ce>             Order. This entitles you to learn some of our secrets,
<ce>             but secrets are also a burden. I do not doubt that
<ce>             you will willingly step forward to shoulder your share.
<ce>             
<ce>             Our Order has duties that are not to be spoken of. The
<ce>             legal system is a creation of men, and is therefore
<ce>             fallible. When that system fails we turn to something
<ce>             more divine - the innate spirit of justice that has
<ce>             drawn all of our brother Knights into this Order.
<ce>             
<ce>             Forgive the flowery language. I will be direct. This
<ce>             Order carries out extra-judicial executions on behalf
<ce>             of a secret group of the most learned, respectable and
<ce>             experienced statesmen in the Kingdom. Judges can be
<ce>             bribed, witnesses can be threatened and lawyers can
<ce>             be...well, don't speak to me of lawyers!
<ce>             
<ce>             Will you be the sword of the spirit of justice?

RefuseQuest:  [1001]
<ce>             It is your decision, of course. But I suggest
<ce>             you avoid making influential enemies within
<ce>             this Order. Just a friendly warning.

AcceptQuest:  [1002]
<ce>             Excellent, it is a great and noble enterprise
<ce>             that we embark on. Crimes will be punished,
<ce>             wrongs will be avenged and future atrocities
<ce>             will never take place, thanks to you.
<ce>             
<ce>             All relevant information is in this letter.
<ce>             The name of the guilty and their last known
<ce>             location.
<ce>             
<ce>             Make no additional notes and destroy the
<ce>             letter when your task is complete.
<ce>             Sometimes agents of the light must work
<ce>             in the shadows.

QuestFail:  [1003]
<ce>             It is a dark day for our Order.
<ce>             And for you in particular.

QuestComplete:  [1004]
<ce>             You have done well %lev, but,
<ce>             for both our sakes, we shall not
<ce>             speak of this business again.


RumorsDuringQuest:  [1005]
The Fruitloops of Zenither are plotting to declare war on the Paperclip-unfolders of Mara. War is hell.
<--->
The Fruitloops of Zenither are plotting to declare war on the Paperclip-unfolders of Mara. War is hell.

RumorsPostfailure:  [1006]
I heard you are an enormous failure. Nice to meet you.
<--->
I heard you are an enormous failure. Nice to meet you.

RumorsPostsuccess:  [1007]
The Cartographers are excited. I wonder why? Probably something to do with maps, I guess.
<--->
The Cartographers are excited. I wonder why? Probably something to do with maps, I guess.

QuestorPostsuccess:  [1008]
Hail fair dispenser of justice!

QuestorPostfailure:  [1009]
We have no further business. Leave me.


Message:  1040
%qdt:
 _questgiver_ in ___questgiver_
 gave me explict instructions not to make
 any notes about this quest. So I won't.

Message:  1041
%qdt:
 My orders have been executed.
 Time to head back.


Message:  1060
<ce>             You have slain the target. It is done.
<ce>             
<ce>             Let's hope the wise, noble statesmen
<ce>             who gave this order didn't make a mistake...

Message: 1061
<ce>             "Yes, I am _npc_. What of it?
<ce>             
<ce>             "Justice? What are you talking about?
<ce>             I was acquitted of those baseless rumours.
<ce>             I had nothing to do with those jurors
<ce>             being poisoned! It was a co-incidence!
<ce>             
<ce>             "Somebody help me! I'm being attacked!"
<--->
<ce>             "_npc_, at your service, %ra.
<ce>             
<ce>             "What's that you said? 'Cheating justice'?
<ce>             I have no idea what you are talking about.
<ce>             I was honourably acquitted by a jury of
<ce>             my peers.
<ce>             
<ce>             "So what if most of them were members of
<ce>             my own family. It was a very small village!
<ce>             And I have a very large family!
<ce>             
<ce>             "Help! Somebody help me!"
<--->
<ce>             "I told the Chief Justice and the Officer
<ce>             of the Watch. And now I'm telling you.
<ce>             You can't pin anything on me.
<ce>             
<ce>             "Anyways, the judge's daughter turned up
<ce>             safe and sound didn't she? No harm done.
<ce>             
<ce>             "What are you doing?! No! You can't do
<ce>             this! Somebody help me!"

Message: 1062
<ce>             There's a flash of light and _npc_
<ce>             has vanished! Must have had an illusion spell...
<ce>             
<ce>             Invisibility or not, they're still around here
<ce>             somewhere. Sooner or later the spell will wear off.
<ce>             Find them and finish this.
<--->
<ce>             Before you can react _npc_ flings a
<ce>             handful of dust in your face! You are blinded
<ce>             for several seconds but quickly recover.
<ce>             
<ce>             _npc_ couldn't have gotten far.
<ce>             They're still very close by. Find them and
<ce>             put an end to this.


Message: 1071
<ce>             _npc_
<ce>             ___dungeon1_

Message: 1072
<ce>             _npc_
<ce>             ___dungeon2_

Message: 1073
<ce>             _npc_
<ce>             ___temple_

Message: 1074
<ce>             _npc_
<ce>             ___palace_

Message: 1075
<ce>             _npc_
<ce>             ___house_



QBN:

Person _questgiver_ group Questor male
Person _npc_ group Resident1 male

Foe _target_ is Warrior

Item _letter1_ letter used 1071
Item _letter2_ letter used 1072
Item _letter3_ letter used 1073
Item _letter4_ letter used 1074
Item _letter5_ letter used 1075

Place _dungeon1_ remote dungeon3
Place _dungeon2_ remote dungeon16
Place _temple_ remote temple
Place _palace_ remote palace
Place _house_ remote house2

Clock _timeLimit_ 90.00:00


--	Quest start-up:
	start timer _timeLimit_
	pick one of _choice1_ _choice2_ _choice3_ _choice4_ _choice5_
	log 1040 step 0


variable _choice1_
variable _choice2_
variable _choice3_
variable _choice4_
variable _choice5_

_mission1_ task:
	when _choice1_
	get item _letter1_ 
	reveal _dungeon1_
	place npc _npc_ at _dungeon1_

_mission2_ task:
	when _choice2_
	get item _letter2_ 
	reveal _dungeon2_
	place npc _npc_ at _dungeon2_

_mission3_ task:
	when _choice3_
	get item _letter3_
	place npc _npc_ at _temple_

_mission4_ task:
	when _choice4_
	get item _letter4_
	place npc _npc_ at _palace_

_mission5_ task:
	when _choice5_
	get item _letter5_
	place npc _npc_ at _house_

_identifyTarget_ task:
	clicked npc _npc_
	say 1061
	hide npc _npc_
	say 1062
	create foe _target_ every 0 minutes 1 times with 100% success

_slain_ task:
	killed _target_
	say 1060
	log 1041 step 1

_questorClicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _questorClicked_ and not _slain_
	clear _questorClicked_ _clearclick_

_rewardPlayer_ task:
	when _questorClicked_ and _slain_
	say 1004
	end quest
