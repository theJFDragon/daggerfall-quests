Quest: JF000019
DisplayName: Jar Jar High-jinks

QRC:

QuestorOffer:  [1000]
<ce>             Excuse me, %ra? Can you spare a moment?
<ce>             Perhaps you could help someone less fortunate than
<ce>             yourself?
<ce>             
<ce>             You can? Oh, thank you! You see...I have this jar
<ce>             of fruit preserve that was hand-crafted by my
<ce>             dear old mother herself, bless her soul. It's an
<ce>             old family recipe, you see. Quite the delicacy.
<ce>             
<ce>             Anyway, I'm having trouble getting the lid off.
<ce>             I'm sure a strong, young %pct like you
<ce>             will have no problems opening an old jar.
<ce>             
<ce>             Can you try?

RefuseQuest:  [1001]
<ce>             Oh. Well, I guess I'll ask somebody else then.

AcceptQuest:  [1002]
<ce>             Thankyou, %ra! Here it is, I'm sure you'll
<ce>             have that lid unscrewed in no time!

QuestFail:  [1003]
<ce>             Oh! I don't know what to say! Probably
<ce>             you're very tired from a long day's
<ce>             adventuring?
<ce>             
<ce>             Don't worry, I won't tell anyone.

QuestComplete:  [1004]
<ce>             Perfect! Thankyou so much! Ah, just smell
<ce>             that delightful aroma. I can't wait to
<ce>             spread that on some sweetroll and get it
<ce>             down my throat!
<ce>             
<ce>             I bet you thought that opening that jar
<ce>             would summon a genie or a daedra or
<ce>             some such thing, eh?! Nope, it's just
<ce>             a perfectly ordinary jar of the finest
<ce>             fruit preserve Tamriel has ever seen.
<ce>             
<ce>             Thanks again, and safe journey brave hero!


RumorsDuringQuest:  [1005]
The Fruitloops of Zenither are plotting to declare war on the Paperclip-unfolders of Mara. War is hell.
<--->
The Fruitloops of Zenither are plotting to declare war on the Paperclip-unfolders of Mara. War is hell.

RumorsPostfailure:  [1006]
I heard you are an enormous failure. Nice to meet you.
<--->
I heard you are an enormous failure. Nice to meet you.

RumorsPostsuccess:  [1007]
The Cartographers are excited. I wonder why? Probably something to do with maps, I guess.
<--->
The Cartographers are excited. I wonder why? Probably something to do with maps, I guess.

QuestorPostsuccess:  [1008]
Hail friend! I'm sorry to report that I have no more jars that need opening.

QuestorPostfailure:  [1009]
You already failed to open my jar. All that fruity goodness is locked away forever!


Message:  1040
%qdt:
 A random peasant has asked me very politely
 to open a jar of fruit preserve. Apparently
 the top has been screwed on very tightly and
 they lack the physical prowess to open it
 by themselves.

 The jar is in my inventory, in case it was
 not abundantly clear.


Message:  1060
<ce>             It was pretty tight, probably due to the
<ce>             old jam going hard. But it was no match
<ce>             for you!
<ce>             
<ce>             The jar opens with a satisfying *pop*!

Message:  1061
<ce>             After several seconds of embarrassing,
<ce>             nay, undignified panting and straining
<ce>             you are forced to admit defeat.

Message:  1062
<ce>             Oh no! That jar was actually a magical
<ce>             prison holding the restrained spirit
<ce>             of an evil daedra!
<ce>             
<ce>             And you just released him into Tamriel!

Message:  1063
<ce>             Phew! The daedroth is dead. Sadly this
<ce>             means that there is no fruit preserve
<ce>             for anybody to enjoy...


QBN:

Person _questgiver_ group Questor male anyInfo 1070

Item _jar_ decanter

Foe _Daedroth_ is Daedroth

Clock _timeLimit_ 0.30:00


--	Quest start-up:
	start timer _timeLimit_
	pick one of _choice1_ _choice2_ _choice4_ _choice5_
	pick one of _choice6_ _choice7_ _choice9_ _choice10_
	get item _jar_
	_jar_ used do _AttemptToOpenJar_
	log 1040 step 0

variable _choice1_
variable _choice2_
variable _choice4_
variable _choice5_
variable _choice6_
variable _choice7_
variable _choice9_
variable _choice10_

_ofCourseItIs_ task:
	when _choice5_ and _choice10_

_timeLimit_ task:
	end quest

_OpenJarSuccess_ task:
	when _AttemptToOpenJar_ and _strengthTest_
	
_OpenJarEasy_ task:
	when _OpenJarSuccess_ and not _ofCourseItIs_
	say 1060
	change repute with _questgiver_ by +5
	give pc nothing
	end quest

_OpenJarWithHilariousSurprise_ task:
	when _OpenJarSuccess_ and _ofCourseItIs_
	create foe _Daedroth_ every 0 minutes 1 times with 100% success
	say 1062

_deaded_ task:
	killed _Daedroth_
	say 1063
	change repute with _questgiver_ by +15
	give pc nothing
	end quest

_OpenJarFailure_ task:
	when _AttemptToOpenJar_ and not _strengthTest_
	say 1061
	say 1003
	end quest

_AttemptToOpenJar_ task:

_strengthTest_ task:
	when attribute Strength is at least 50
