Quest: JF000006
DisplayName: Rent-a-Ghost

QRC:

QuestorOffer:  [1000]
<ce>             %pcf, thank %god it's you!  There's a little
<ce>             task that needs doing and you'd surely be the
<ce>             perfect person to handle it.
<ce>             
<ce>             I should warn you that this business is, how
<ce>             to say, legally ambiguous? Morally opaque?
<ce>             Ethically vague. Definitely NOT criminal!
<ce>             
<ce>             There's no actual danger, of course. Unless
<ce>             something goes wrong. Then it could be very
<ce>             dangerous indeed.
<ce>             But when does that ever happen?
<ce>             
<ce>             Shall I say more?


RefuseQuest:  [1001]
<ce>             With that attitude your future in the
<ce>             Mages Guild seems pretty static.


AcceptQuest:  [1002]
<ce>             Splendid! It's simple enough, really. We have a special
<ce>             arrangement, more of an understanding really, with the
<ce>             local Fighters Guild. One of their main sources of
<ce>             income is dealing with unwanted vermin. Tigers, bears,
<ce>             rats, that sort of thing.
<ce>             
<ce>             Unfortunately for them there's not a lot of money in
<ce>             such...conventional...house cleaning. What really brings
<ce>             in the coin is super-natural infestations. Wraiths,
<ce>             ghosts and zombies are a lot more expensive to handle.
<ce>             
<ce>             Of course these encounters don't happen every day. Some
<ce>             towns aren't even built on top of old burial grounds!
<ce>             That's where we come in. For a fair and reasonable
<ce>             percentage we provide the actors and then the Fighters
<ce>             Guild takes care of them. Everyone goes home happy!
<ce>             
<ce>             I just need you to go to _someHouse_
<ce>             and use this enchanted gem of conjuration. Once the
<ce>             spell is cast come back here. Do not engage the
<ce>             conjured creatures, naturally.


QuestFail:  [1003]
<ce>             Disappointing %pcf. The Guild has a
<ce>             reputation to maintain and yours just
<ce>             took a right beating. Get out of here.


QuestComplete:  [1004]
<ce>             Well...here's your share. But let's say no
<ce>             more about this, %pcf.


RumorsDuringQuest:  [1005]
I'm hearing lots more talk of hauntings and supernatural goings-on these days. Curious, eh?
<--->
I thought it was only Daggerfall city having problems with the undead. But it's getting a bit unlively around here, if you catch my drift.


RumorsPostfailure:  [1006]
I don't know the details but the local Mages and Fighters Guilds seem a little more unfriendly these days.
<--->
I hear whispers of illegal conspiracies but I can't say any more than that. People should keep their mouths shut.


RumorsPostsuccess:  [1007]
How am I doing? Me? I'm doing very well, thanks for asking! It's nice not be constantly asked for directions for once!


QuestorPostsuccess:  [1008]
Why, if it isn't %pcf, my partner-in-crime. As it were. So to speak. *Ahem*.


QuestorPostfailure:  [1009]
Hmmm. I've been asked to keep the...sensitive jobs for our more reliable members.


Message:  1040
%qdt:
 _questgiver_ in ___questgiver_
 has got me working on a genius scam!
 The Mages Guild conjure up a host
 of ghosts and then the Fighters
 Guild get paid to take care of them.
 Then we split the profits!
 I need to conjure some angry
 spirits in _someHouse_
 using this enchanted gemstone.


Message:  1041
<ce>             This is the place. Time to bring
<ce>             back the dead for fun and profit!


Message:  1042
 The deed is done! Now to head
 back to the Mages Guild. I am
 totally certain that nothing
 unexpected or untoward could
 possibly have happened...


Message: 1043
<ce>             The gem glows very brightly and then goes dull.
<ce>             Your advanced Mage training and extensive
<ce>             experience leads you to conclude that the
<ce>             spell has been successfully cast.
<ce>             
<ce>             The sudden drop in temperature, a feeling of
<ce>             intense dread and the sudden appearance of
<ce>             horrifying spectral apparitions are also strong
<ce>             hints that you should probably leave now...


Message:  1050
<ce>             I'm not sure what you did but
<ce>             it wasn't entirely correct. It
<ce>             seems we have a few uninvited
<ce>             guests ourselves. Take care of
<ce>             this, would you, %pcf?


QBN:

Item _gold_ gold range 250 to 500
Item _gem_ gem

Person _questgiver_ group Questor male

Foe _wraith_ is 3 Wraith
Foe _wraith2_ is 1 Wraith

Place _someHouse_ local house2
Place _magesGuild_ local magery

Clock _timeLimit_ 1.00:00


--	Quest start-up:
	start timer _timeLimit_
	get item _gem_ 
	pc at _someHouse_ set _onsite_
	log 1040 step 0


_onsite_ task:
	say 1041
	_gem_ used do _useGem_

_useGem_ task:
	say 1043
	log 1042 step 2
	create foe _wraith_ every 0 minutes 1 times with 100% success

_slain_ task:
	killed 1 _wraith2_

_timeLimit_ task:
	end quest

_questorClicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _questorClicked_ and not _useGem_
	clear _questorClicked_ _clearclick_

_oops_ task:
	when _questorClicked_ and _useGem_ and not _slain_
	create foe _wraith2_ every 0 minutes 1 times with 100% success
	say 1050

_rewardPlayer_ task:
	when _questorClicked_ and _slain_
	give pc _gold_
	end quest
