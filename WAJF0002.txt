Quest: WAJF0002
DisplayName: Special Delivery

QRC:


QuestorOffer:  [1000]
<ce>             Well now, if it isn't %pcf %g2self.
<ce>             Always showing up at the perfect time.
<ce>             And always ready to do a good deed
<ce>             for a friend of a friend, no?
<ce>             
<ce>             Listen, there's some decent coin in
<ce>             this one. We've got to deliver a
<ce>             few things to __alchemyShop_ but
<ce>             the Watch is on to us. I reckon
<ce>             you could slip by them?
<ce>             
<ce>             Like I say...lots of coin. You game?
<--->
<ce>             I know you, you're %pcf, right?
<ce>             You'd be just perfect to help us out
<ce>             with this delivery job we've got.
<ce>             
<ce>             Some special stuff needs to get to
<ce>             __alchemyShop_ but the Watch are
<ce>             out and about and they know our
<ce>             faces. A stranger such as yourself
<ce>             would have no such problem.
<ce>             
<ce>             There's plenty of gold in it, naturally.
<ce>             Are you in?


RefuseQuest:  [1001]
<ce>             Maybe next time then, %ra?

AcceptQuest:  [1002]
<ce>             Good. Just keep calm and absolutely
<ce>             nothing can go wrong.
<ce>             
<ce>             Just get over to __alchemyShop_
<ce>             and find _alchemyShop_.
<ce>             
<ce>             Your contact is _theContact_.
<ce>             
<ce>             That's all there is. Simple.
<ce>             
<ce>             Hey, don't forget the _theStuff_!

QuestFail:  [1003]

QuestComplete:  [1004]



Message:  1022
<ce>             Greetings citizen. The Watch is conducting
<ce>             a special inspection as part of the ongoing
<ce>             fight against smuggling and the trafficking
<ce>             of illegal and stolen merchandise.
<ce>             
<ce>             However, you are clearly an upright and
<ce>             law-abiding sort so you are free to go about
<ce>             your business. Move along.


Message:  1023
<ce>             Hold it right there, scum! This is a City
<ce>             Watch inspection and you're just the type
<ce>             of criminal filth we're looking for.
<ce>             
<ce>             I thought so...caught red-handed. Looks like
<ce>             another Thieves Guild renegade died trying
<ce>             to resist arrest!


Message:  1044
<ce>             That seems to be in order. A pleasure doing
<ce>             business with you. Now I must take my leave...


Message:  1066
<ce>             Halt! This is the City Watch! All travellers
<ce>             will submit to a search for illegal contraband.


Message:  1067
<ce>             "That's the one! Arrest %pg2 at once!"
<ce>             "Halt! Halt! Halt!"
<ce>             
<ce>             The City Watch has laid a trap! Run!
<ce>             
<ce>             "Halt!"


Message:  1068
<ce>             "That's the one! Cut %pg3 throat right quick and get the stuff!"
<ce>             
<ce>             Seems you've been set up by a rival gang. Good luck!


Message:  1069
<ce>             Everything seems fine. Just find the contact,
<ce>             hand over the stuff and get out of here.


Message:  1040
%qdt:
 Get to __alchemyShop_.
 Find _alchemyShop_.
 Look for _theContact_.
 Deliver the stuff.
 Return to ___questgiver_.
 Report to _questgiver_.


Message:  1041
%qdt:
 The City Watch didn't suspect a thing and
 let me slip right through their net.
 

Message:  1042
%qdt:
 The City Watch busted me! There's no way
 to complete this delivery now. The deal's off!
 Or maybe it would be better to just
 disppear for a while?


QBN:

Person _questgiver_ group Questor male
Person _theContact_ faction The_Merchants female

Item _gold_ gold range 500 to 500
Item _theStuff_ drug

Place _alchemyShop_ remote apothecary

Clock _timeLimit_ 10.00:00

Foe _warriorEnemy_ is 4 Warrior
Foe _singleWarriorEnemy_ is Warrior


_offer_ task:
	prompt 1000 yes _accept_ no _decline_

_accept_ task:
	say 1002
	start timer _timeLimit_
	get item _theStuff_ 
	pc at _alchemyShop_ set _inThePlace_
	pick one of _inspection_ _allIsOK_ _setup_ _ambush_
	log 1040 step 0

_decline_ task:
	end quest saying 1001

variable _skillTestOK_
variable _inThePlace_
variable _inspection_
variable _allIsOK_
variable _setup_
variable _ambush_

_entersCity_ task:
	when pc enters city

_startInspection_ task:
	when _inspection_ and _entersCity_
	create foe _warriorEnemy_ every 0 minutes 1 times with 100% success
	say 1066

_etiquette_ task:
	when skill Etiquette is at least 35
	start task _skillTestOK_

_streetwise_ task:
	when skill Streetwise is at least 35
	start task _skillTestOK_

_personality_ task:
    when attribute Personality is at least 70
    start task _skillTestOK_

_SkillCheckPassed_ task:
    when _startInspection_ and _skillTestOK_
	restrain foe _warriorEnemy_
    say 1022
	log 1041 step 1

_SkillCheckFailed_ task:
    when _startInspection_ and not _skillTestOK_
    say 1023
	log 1042 step 2
	end quest

_inThePlaceOK_ task:
	when _inThePlace_ and _allIsOK_
	place npc _theContact_ at _alchemyShop_	
	say 1069

_inThePlaceSetup_ task:
	when _inThePlace_ and _setup_
	place foe _singleWarriorEnemy_ at _alchemyShop_
	create foe _warriorEnemy_ every 0 minutes 1 times with 100% success
	say 1067
	end quest

_inThePlaceAmbush_ task:
	when _inThePlace_ and _ambush_
	place foe _singleWarriorEnemy_ at _alchemyShop_
	create foe _warriorEnemy_ every 0 minutes 1 times with 100% success
	say 1068
	end quest

_deliverTheStuff_ task:
	toting _theStuff_ and _theContact_ clicked 

_stuffDelivered_ task:
	when _deliverTheStuff_
	say 1044
	hide npc _theContact_ 
	end quest

_timeLimit_ task:
	end quest
