Quest: JF000003
DisplayName: Runaround

QRC:

QuestorOffer:  [1000]
<ce>             Hail %lev! Your timing is, as
<ce>             ever, fortuitous. We have urgent need of
<ce>             a Knight whose steed is as fast as their
<ce>             wits, if not faster.
<ce>             
<ce>             The Thieves Guild, curse their empty
<ce>             hearts, have taken a local noble hostage.
<ce>             We know this particular gang and they are
<ce>             no mere amateurs. Rest assured - if they
<ce>             are crossed then the hostage will die.
<ce>             
<ce>             Your duty, if you accept, is to follow
<ce>             their instructions and deliver the ransom.
<ce>             Do you accept this duty?


RefuseQuest:  [1001]
<ce>             You disappoint us %lev.
<ce>             If we do not undertake these quests then who will?


AcceptQuest:  [1002]
<ce>             I know we could count on you %lev.
<ce>             
<ce>             This gang of kidnappers is well organised and
<ce>             paranoid. They will permit you no leeway in their
<ce>             plans. You will be sent around town, from place
<ce>             to place, to make sure you're alone. If they think
<ce>             you've got any compatriots they'll cancel the deal,
<ce>             which will be unfortunate for the hostage.
<ce>             
<ce>             Your first destination is _house_ here
<ce>             in town. After that you're on your own.
<ce>             
<ce>             Do not deviate from their instructions. Just give
<ce>             them their ill-begotten ransom.
<ce>             
<ce>             Good luck %pcf.


QuestFail:  [1003]
<ce>             Disappointing %pcf. The Order has a reputation
<ce>             to maintain and yours just took a right beating.
<ce>             Get out of my sight.


QuestComplete:  [1004]
<ce>             Nice work, these things don't always go smoothly.
<ce>             Here's your reward, you certainly earned it!


RumorsDuringQuest:  [1005]
Do you have time to talk about the weather? Don't you have something to do?


RumorsPostfailure:  [1006]
I wonder what happened to the Baroness? Not seen her for a while.


RumorsPostsuccess:  [1007]
The Baroness seems happier these days. At least somebody around here has had some good news. 


QuestorPostsuccess:  [1008]
Greetings %pcf. I was just telling someone about how you helped me sort out that ransom problem.


QuestorPostfailure:  [1009]
If I was you I would avoid high society for a long while. Still, I guess for you that's not much to ask.


Message:  1040
%qdt:
 _questgiver_ in ___questgiver_
 has engaged me to deliver a ransom to
 some Thieves Guild operatives.
 They first want me to go to
 _house_.


Message:  1041
 _inn_.
 You got 35 minutes. Do not tarry.


Message:  1042
 _shop_.
 30 minutes.
 I hope you've got a good horse.


Message:  1043
 _pawnshop_.
 25 minutes.
 Hurry hurry hurry!


Message:  1044
<ce>             "It's been a pleasure doing business.
<ce>             The merchandise will be delivered directly
<ce>             and in perfect condition."


Message:  1045
<ce>             You've run out of time! The Thieves Guild will
<ce>             still deliver the hostage. Just in many, many,
<ce>             small pieces over a long period of time...


Message:  1051
%qdt:
 The next place to go is  _inn_.


Message:  1052
%qdt:
 The next place is  _shop_.


Message:  1053
%qdt:
 The last place is  _pawnshop_.


Message:  1054
%qdt:
 The ransom is paid. Time to head back
 and collect my payment.


QBN:

Item _gold_ gold range 250 to 500

Item _note1_ letter used 1041
Item _note2_ letter used 1042
Item _note3_ letter used 1043
Item _bag_ large_sack

Person _questgiver_ group Questor male
Person _kidnapper_ face 36 faction The_Thieves_Guild

Place _house_ local house2
Place _inn_ local tavern
Place _shop_ local generalstore
Place _pawnshop_ local pawnshop

Clock _timeLimit_ 1.00:00
Clock _timer1_ 0.0:35
Clock _timer2_ 0.0:30
Clock _timer3_ 0.0:25

Foe _bandit_ is Rogue


--	Quest start-up:
	start timer _timeLimit_
	_note1_ used do _readNote1_ 
	_note2_ used do _readNote2_ 
	_note3_ used do _readNote3_ 
	place item _note1_ at _house_
	get item _bag_
	log 1040 step 0


_timeLimit_ task:
	end quest

_timer1 task:
	say 1045
	end quest

_timer2_ task:
	say 1045
	end quest

_timer3_ task:
	say 1045
	end quest

_readNote1_ task:
	log 1051 step 1
	place item _note2_ at _inn_
	start timer _timer1_

_readNote2_ task:
	log 1052 step 2
	stop timer _timer1_
	place item _note3_ at _shop_
	start timer _timer2_

_readNote3_ task:
	log 1053 step 3
	stop timer _timer2_
	place npc _kidnapper_ at _pawnshop_
	start timer _timer3_

_dieHard3_ task:
	stop timer _timer3_
	toting _bag_ and _kidnapper_ clicked 

_giveRansom_ task:
	when _dieHard3_
	say 1044
	log 1054 step 4
	hide npc _kidnapper_ 

_questorClicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _questorClicked_ and not _giveRansom_
	clear _questorClicked_ _clearclick_

_rewardPlayer_ task:
	when _questorClicked_ and _giveRansom_
	give pc _gold_
	end quest
